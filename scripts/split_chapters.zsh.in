#!@ZSH@
set -e

source "$CASILEDIR/scripts/functions.zsh"

alias pandoc="@PANDOC@"
alias sponge="@SPONGE@"
alias sed="@SED@"
alias mkdir="@MKDIR_P@"

splitlevels=$1
test -n $splitlevels

bookid=$2
test -n $bookid

# TODO: actually support --top-level=part where parts are another level of splits
part_no=0
chapter_no=0
section_no=0
of=$bookid.md
files=()

mkdir $BUILDDIR
src=$(mktemp $BUILDDIR/splitXXXXXX.md)
trap 'rm -rf $src' EXIT SIGHUP SIGTERM

mv $bookid.md $src
truncate -s0 $bookid.md

pandoc $src --markdown-headings=atx --wrap=none --to=markdown --reference-location=document |
while read -r line; do
	# Stop processing if we've hit footnotes
	[[ $line =~ "^\[\^1\]: .*" ]] && break

	# Check for headers
	if [[ $line =~ '^\\part\{.*\}$' ]]; then
		of=$bookid.md
	elif [[ $line =~ "^#+ .*" ]]; then
		if [[ $line =~ "^# " ]]; then
			section_no=0
			[[ $line =~ ".*unnumbered.*" ]] || let chapter_no=$chapter_no+1
			dir=${bookid}-chapters
			mkdir $dir
			of=$dir/$(printf %03d $chapter_no).md
			files+=($of)
			>> $bookid.md <<< "esyscmd([[loadchapters.zsh \"$of\"]])"$'\n'
		elif [[ $line =~ "^## " ]] && [[ $splitlevels -le 2 ]] then
			[[ $line =~ ".*unnumbered.*" ]] || let section_no=$section_no+1
			dir=${bookid}-chapters/$(printf %03d $chapter_no)-sections/
			mkdir $dir
			of=$dir/$(printf %03d $section_no).md
			files+=($of)
			>> $bookid.md <<< "esyscmd([[loadchapters.zsh \"$of\"]])"$'\n'
		elif [[ $splitlevels -ge 3 ]] then
			flunk 'Unsupported 3 level split'
		fi
	fi

	>> $of <<< "$line"
done

# Truncate source to just footnotes
sed -i -n -e '/^\[\^1\]: /,$p' $src

# Put footnotes in all files, renumber, and generally cleanup
for file in $files; do
	>> $file < $src
	normalize_pandoc < $file |
		sponge $file
	track $file
done

track $bookid.md
commit "Split $bookid up into chapters and sections"

"$CASILEDIR/scripts/normalize_files.zsh" $bookid
